apply plugin: 'com.android.library'

def artifactProperties = new Properties()
artifactProperties.load(new FileInputStream("$project.projectDir/deploy.settings"))

group artifactProperties.groupId
version artifactProperties.version

android {
    compileSdkVersion 26

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 23
        versionCode 1
        versionName "${version}"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

dependencies {
    if (project.hasProperty('masterProject') == false || project.property('release') > 0) {
        implementation("com.highmobility:hmkit-android:${artifactProperties.hmkitVersion}@aar") {
            transitive = true
        }
    } else {
        implementation project(path: ':hmkit-android')
        implementation project(path: ':hm-java-crypto')
        implementation project(path: ':hm-java-utils')
    }

    implementation('com.highmobility:hmkit-auto-api:1.8.0')

    implementation 'com.android.support:support-v4:26.1.0'
    implementation 'info.hoang8f:android-segmented:1.0.6'

    implementation('com.google.code.findbugs:jsr305:3.0.2')

    testImplementation 'junit:junit:4.12'
}

apply plugin: 'com.jfrog.artifactory'
apply plugin: 'digital.wup.android-maven-publish'

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

publishing {
    publications {
        aar(MavenPublication) {
            from components.android
            groupId artifactProperties.groupId
            // for bintray: https://github.com/bintray/gradle-bintray-plugin
        }
    }
}

// artifactory
artifactory {
    contextUrl = System.properties['hmArtifactoryUrl']

    publish {
        repository {
            repoKey = artifactProperties.repo
            username = System.properties['hmArtifactoryPublishUser']
            password = System.properties['hmArtifactoryPublishPassword']
            maven = true
        }

        defaults {
            publishArtifacts = true
            publications('aar')
            publishIvy = false
            publishPom = true
        }
    }
}