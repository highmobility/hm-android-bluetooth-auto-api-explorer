apply plugin: 'com.android.library'

group "com.highmobility"
version "1.7.1"

android {
    compileSdkVersion = rootProject.ext.compileSdkVersion

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion rootProject.ext.compileSdkVersion
        versionCode 1
        versionName "${version}"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
}

dependencies {
    if (project.hasProperty('releaseType') == false || project.releaseType > 0) {
        implementation("com.highmobility:hmkit-android:${project.hmkitVersion}@aar") {
            transitive = true
        }
        implementation("com.highmobility:hmkit-auto-api:${project.autoApiVersion}")
    } else {
        implementation project(path: ':hmkit-android')
        implementation project(path: ':hmkit-crypto')
        implementation project(path: ':hmkit-utils')
        implementation("com.highmobility:hmkit-auto-api:${rootProject.ext.autoApiVersion}") {
            exclude group: 'com.highmobility', module: 'hmkit-utils'
        }
    }

    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'info.hoang8f:android-segmented:1.0.6'
    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation('com.google.code.findbugs:jsr305:3.0.2')

    testImplementation 'junit:junit:4.12'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}